plugins {
	id 'org.springframework.boot' version '2.1.6.RELEASE'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'eclipse'
group = 'com.ies.curso'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

//JAXB
task genJaxb(group:'build',description:'Construye clases jaxb') {
	ext.sourcesDir = "src/generated/jaxb"
	ext.classesDir = "${buildDir}/classes/jaxb"
	ext.schema = "src/main/wsdl/WSBovedaContrato.wsdl"

	outputs.dir classesDir

	doLast() {
		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
					classpath: configurations.jaxb.asPath
			mkdir(dir: sourcesDir)
			mkdir(dir: classesDir)

			xjc(destdir: sourcesDir, schema: schema,
				package: 'com.ies.curso.spring.tema07.ejemplo01.dto') {
				arg(value: "-wsdl")
				produces(dir: sourcesDir, includes: "**/*.java")
			}

			javac(destdir: classesDir, source: 1.6, target: 1.6, debug: true,
					debugLevel: "lines,vars,source",
					classpath: configurations.jaxb.asPath) {
				src(path: sourcesDir)
				include(name: "**/*.java")
				include(name: "*.java")
			}

			copy(todir: classesDir) {
				fileset(dir: sourcesDir, erroronmissingdir: false) {
					exclude(name: "**/*.java")
				}
			}
		}
	}
}

task afterEclipseImport {
	dependsOn "genJaxb"
}

configurations {
	jaxb
}
//END JAXB

dependencies {
	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
	testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
	compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
	testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
	
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	
	//Spring MVC
	implementation("org.springframework.boot:spring-boot-starter-web")
	implementation("org.springframework.boot:spring-boot-starter-thymeleaf")
	implementation("org.springframework.boot:spring-boot-devtools")
	implementation("org.hibernate.validator:hibernate-validator")
	implementation("org.apache.tomcat.embed:tomcat-embed-el")
	// REST
	implementation("org.springframework.boot:spring-boot-starter-hateoas")
	
	//INTEGRATION TESTING
	testImplementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.9'
	testImplementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.11'
	testImplementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.9'

	//SOAP
	implementation("org.springframework.boot:spring-boot-starter-web-services")
	implementation("wsdl4j:wsdl4j:1.6.1")
	implementation(files(genJaxb.classesDir).builtBy(genJaxb))
	
	//JAXB
	jaxb("org.glassfish.jaxb:jaxb-xjc:2.2.11")
}

